plugins {
  id 'java'
  id 'maven'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '4.0.4'
}

ext {
  group = 'meghanada'
  serverVersion = '1.0.13'
  buildVersion = 'release'

  def gitFile = new File('./.git')
  if (gitFile.exists()) {
    branch = grgit.branch.current().name
    if (!["master", "dev"].contains(branch)) {
      serverVersion = "$serverVersion-${branch}"
    }
    buildVersion = grgit.head().abbreviatedId
  }
  longVersion = "$serverVersion-${buildVersion}"
  date = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")

  junitVersion = '5.4.0'
  gradleVersion = '5.2.1'
  log4jVersion = '2.11.2'
  xodusVersion = '1.3.0'
}

mainClassName = "meghanada.Main"
applicationName = 'meghanada'

// compileJava {
//   options.compilerArgs += ["--add-exports", "jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
//                            "--add-exports", "jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
//                            "--add-exports", "jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
//                            "--add-exports", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED"]
// }

// compileTestJava {
//   options.compilerArgs += ["--add-exports", "jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
//                            "--add-exports", "jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
//                            "--add-exports", "jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
//                            "--add-exports", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED"]
// }


tasks.withType(JavaCompile) {
  options.incremental = true
}

shadowJar {
  mergeServiceFiles()
  baseName = 'meghanada'
  classifier = null
  version = serverVersion
  relocate 'junit.framework', 'meghanada.junit.framework'
  relocate 'com.github.javaparser', 'meghanada.com.github.javaparser'
  relocate 'com.google', 'meghanada.com.google'
  relocate 'org.objectweb.asm', 'meghanada.org.objectweb.asm'
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publish = true
  // override = true

  filesSpec {
    from 'build/libs'
    into '.'
  }

  pkg {
    repo = 'meghanada'
    name = 'meghanada'
    vcsUrl = 'https://github.com/mopemope/meghanada-server.git'
    licenses = ['GPL-3.0']
    githubRepo = 'mopemope/meghanada-server'
    githubReleaseNotesFile = 'README.md'
    labels = ['java', 'emacs']

    version {
      name = "$serverVersion"
      desc = "Meghanada Server $longVersion"
      released  = new Date()
    }
  }
}

dependencies {

  compile files("${System.properties['java.home']}/../lib/tools.jar")
  compile 'com.google.googlejavaformat:google-java-format:1.7'
  compile 'org.apache.maven:maven-model-builder:3.6.0'
  compile 'com.leacox.motif:motif:0.1'
  compile 'com.leacox.motif:motif-hamcrest:0.1'
  compile 'com.github.javaparser:javaparser-core:3.7.1'
  compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
  compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
  compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
  compile 'commons-cli:commons-cli:1.4'
  compile "org.gradle:gradle-tooling-api:$gradleVersion"
  compile 'com.google.guava:guava:27.0.1-jre'
  compile 'org.ow2.asm:asm:7.0'
  compile 'com.typesafe:config:1.3.3'
  compile 'org.atteo:evo-inflector:1.2.2'

  compile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  compile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
  compile "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
  compile "org.junit.vintage:junit-vintage-engine:$junitVersion"
  compile 'org.junit.platform:junit-platform-launcher:1.4.0'

  compile 'com.android.tools.build:builder-model:3.0.1'
  compile 'io.takari.junit:takari-cpsuite:1.2.7'
  compile 'org.jboss.windup.decompiler:decompiler-api:4.2.0.Final'
  compile 'org.jboss.windup.decompiler:decompiler-fernflower:4.2.0.Final'
  compile 'com.google.code.findbugs:jsr305:3.0.2'
  compile 'org.eclipse.jdt:org.eclipse.jdt.core:3.16.0'
  compile 'de.ruedigermoeller:fst:2.56'
  compile "org.jetbrains.xodus:xodus-query:$xodusVersion"
  compile "org.jetbrains.xodus:xodus-openAPI:$xodusVersion"
  compile "org.jetbrains.xodus:xodus-vfs:$xodusVersion"
  compile "org.jetbrains.xodus:xodus-utils:$xodusVersion"
  compile "org.jetbrains.xodus:xodus-lucene-directory:$xodusVersion"
  compile "org.jetbrains.xodus:xodus-environment:$xodusVersion"
  compile "org.jetbrains.xodus:xodus-entity-store:$xodusVersion"
  compile "org.jetbrains.xodus:xodus-compress:$xodusVersion"
}


test {
  jvmArgs = ["-XX:SoftRefLRUPolicyMSPerMB=50", "-XX:+UseConcMarkSweepGC", "-Xverify:none"]
  testLogging {
    events "PASSED", "FAILED", "SKIPPED"
    exceptionFormat "full"
  }
}

task installEmacsHome(type: Copy) {
  def home = System.properties['user.home']
  from "build/libs/meghanada-${serverVersion}.jar"
  into "$home/.emacs.d/meghanada/"
}

installEmacsHome.dependsOn(shadowJar)

task embedVersion(type: Copy) {
  from 'src/main/resources/VERSION'
  into 'build/resources/main'

  expand(buildDate: new Date(), version: longVersion, appName: applicationName)
}

classes.dependsOn embedVersion
embedVersion.mustRunAfter processResources

task('createPom') {
  doLast {
    pom {
      project {
        groupId 'meghanada'
        artifactId 'meghanada'
        version "$serverVersion"
      }
    }.writeTo("pom.xml")
  }
}

clean.doLast {
    file('.meghanada').deleteDir()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
