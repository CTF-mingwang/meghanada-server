plugins {
  id 'java'
  id 'maven'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '2.0.3'
}

ext {
  group = 'meghanada'
  setupVersion = '0.0.1'
  buildVersion = 'release'

  def gitFile = new File('./.git')
  if (gitFile.exists()) {
    buildVersion = grgit.head().abbreviatedId
  }
  longVersion = "$setupVersion-${buildVersion}"
  date = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
}

mainClassName = "meghanada.SetupMain"
applicationName = 'meghanada-setup'

dependencies {
  compile 'commons-cli:commons-cli:1.4'
}

tasks.withType(JavaCompile) {
  options.incremental = true
}

task installEmacsHome(type: Copy) {
  def home = System.properties['user.home']
  from "build/libs/meghanada-setup-${setupVersion}.jar"
  into "$home/.emacs.d/meghanada/"
}

installEmacsHome.dependsOn(shadowJar)

task embedVersion(type: Copy) {
  from 'src/main/resources/VERSION'
  into 'build/resources/main'

  expand(buildDate: new Date(), version: longVersion, appName: applicationName)
}

classes.dependsOn embedVersion
embedVersion.mustRunAfter processResources

clean.doLast {
    file('.meghanada').deleteDir()
}

shadowJar {
  baseName = 'meghanada-setup'
  classifier = null
  version = setupVersion
}

configurations.errorprone {
  resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.2.0'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8
